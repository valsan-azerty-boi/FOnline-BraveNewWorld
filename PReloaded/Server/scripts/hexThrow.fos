// shared client/server script for throwing objects
#include "_macros.fos"
#include "MsgStr.h"

#ifdef __SERVER
# define CritterMutual       Critter
# define ItemMutual          Item
# include "utils_h.fos"
# include "combat_h.fos"
import uint GetAnimFlySpeedByAngle(float flyAngle) from "hexShot";
#endif

#ifdef __CLIENT
#include "polygon_h.fos"
# include "sprite.fos"
# include "_colors.fos"
# include "_client_defines.fos"
# define CritterMutual       CritterCl
# define ItemMutual          ItemCl
#endif

#define STR_THROWING_HIT     (10400)
#define STR_THROWING_MISS    (10401)

#define THROWABLE_SIZE      (7)

bool IsHexThrowingWeapon(ItemMutual@ item)
{
    if(!valid(item))
        return false;
    if(item.GetType() != ITEM_TYPE_WEAPON)
        return false;
    uint8 use = _WeaponModeUse(item.Mode);
    return _WeaponSkill(item.Proto, use) == SK_THROWING;
}

uint HexThrowGetMaxRange(CritterMutual& cr, ItemMutual@ item)
{
    if(!valid(item))
        return 0;
    uint bonus = 6 * cr.Perk[PE_HEAVE_HO];
    uint max = 3 * cr.Stat[ST_STRENGTH];
    if(IsGrenade(item) || IsDistractWeapon(item))
    {
        uint8 use = _WeaponModeUse(item.Mode);
        return bonus + MIN(_WeaponMaxDist(item.Proto, use), max);
    }
    else
    {
        return 0;
    }
}

bool IsHexThrowable(ProtoItem& weapon)
{
    if(!valid(weapon))
        false;
    if (weapon.ProtoId == PID_ROCK) {
        return true;
    }
    return !(weapon.Type == ITEM_TYPE_WEAPON && weapon.Weapon_IsUnarmed);
}

int HexThrowingToHit(CritterMutual& cr, uint16 tx, uint16 ty, ProtoItem& weapon)
{
    if(!valid(weapon))
        return 0;
    if(weapon.Type == ITEM_TYPE_WEAPON && weapon.Weapon_IsUnarmed)
        return 0;
    if(!cr.IsLife())
        return 0;

    int dist = GetDistantion(cr.HexX, cr.HexY, tx, ty);

    int toHit = int(cr.Skill[SK_THROWING]);
    int weaponPerk = weapon.Weapon_Perk;
    int blockers = 0;

    // cr.Say(SAY_NETMSG, "Applying range modifiers.");
    // ranged attack modifiers
    int distmod1 = 2;   // used for initial weapon bonus
    int distmod2 = 0;   // minimal distance

    int perception = cr.Stat[ST_PERCEPTION];
    int acc = dist;

    if(dist < distmod2)
        acc += distmod2;
    else
    {
        if(cr.IsPlayer())
            acc -= (perception - 2) * distmod1;                  // we want to keep it, because fo2238 might use it to display NPCs chance (via possessing)
        else
            acc -= perception * distmod1;
    }

    if(-2 * perception > acc)
        acc = -2 * perception;

    acc -= 2 * cr.Perk[PE_SHARPSHOOTER];

    if(acc >= 0)
    {
        if(cr.Damage[DAMAGE_EYE] != 0)
            acc *= -12;
        else
            acc *= -4;
    }
    else
        acc *= -4;

    toHit += acc;

    #ifdef __SERVER
    Map@ map = cr.GetMap();
    blockers = map.GetCrittersPath(cr.HexX, cr.HexY, tx, ty, 0.0f, dist, FIND_LIFE, null);
    #endif
    #ifdef __CLIENT
    blockers = GetCrittersPath(cr.HexX, cr.HexY, tx, ty, 0.0f, dist, FIND_LIFE, null);
    #endif

    toHit -= 10 * blockers;

    if(cr.Damage[DAMAGE_EYE] != 0)
        toHit -= 25;
    toHit = CLAMP(toHit, 5, _MaxToHit(cr));

    return toHit;
}

int HexThrowActionPointsCost(ItemMutual& item, CritterMutual& cr) {
    if (IsGrenade(item) || IsDistractWeapon(item)) {
        uint8 use = _WeaponModeUse(item.Mode);
        int apCost = (cr.Perk[PE_BONUS_RATE_OF_ATTACK]>0)?-1:0;
        apCost += _WeaponApCost(item.Proto, use);

        return apCost;
    }
    return 0;
}

bool IsHexThrowableThere(CritterMutual& cr, ItemMutual@ item, uint16 tx, uint16 ty)
{
    if(!valid(item) || !IsHexThrowable(item.Proto) || !cr.IsLife())
        return false;
    #ifdef __SERVER
    if(cr.Stat[ST_CURRENT_AP] < HexThrowActionPointsCost(item, cr))
        return false;
    if(HexThrowActionPointsCost(item, cr) == 0)
        return false;
    #endif
    if(cr.Damage[DAMAGE_RIGHT_ARM] != 0 && cr.Damage[DAMAGE_LEFT_ARM] != 0)
        return false;
    if(HexThrowGetMaxRange(cr, item) == 0 || GetDistantion(cr.HexX, cr.HexY, tx, ty) == 0)
        return false;

    // wallcheck
    uint16 pbx = tx;
    uint16 pby = ty;
    uint16 bx = tx;
    uint16 by = ty;

    #ifdef __SERVER
    Map@ map = cr.GetMap();
    map.GetCrittersPath(cr.HexX, cr.HexY, tx, ty, 0.0f, 0, 0, null, pbx, pby, bx, by);
    #endif
    #ifdef __CLIENT
    GetCrittersPath(cr.HexX, cr.HexY, tx, ty, 0.0f, 0, 0, null, pbx, pby, bx, by);
    #endif

    if(bx != tx || by != ty)
        return false;

    return HexThrowGetMaxRange(cr, item) >= GetDistantion(cr.HexX, cr.HexY, tx, ty);
    // todo: wall tracing on the client side
}

bool IsDistractWeapon(ItemMutual& item) {
    if (item.GetProtoId() == PID_ROCK) {
        return true;
    }
    return false;
}

bool IsGrenade(ItemMutual& item)
{
    if(!IsHexThrowingWeapon(item))
        return false;
    uint8 use = _WeaponModeUse(item.Mode);
    uint8 dmgType = _WeaponDmgType(item.Proto, use);
    return dmgType == DAMAGE_TYPE_PLASMA || dmgType == DAMAGE_TYPE_EMP || dmgType == DAMAGE_TYPE_EXPLODE || dmgType == DAMAGE_TYPE_FIRE;
}

#ifdef __SERVER

import void CommenceExplosionHexEx(Critter@ attacker, Map@ map, uint16 tx, uint16 ty, Critter@ target, uint weapPid, Item@ usedWeapon, uint ammoPid, bool isCritical, uint intentionallyId, bool isRocket, bool isExplodeRocket) from "combat";

void unsafe_HexThrow(Critter& player, int px, int py, int p2, string@ param3, array<int>@ param4)
{
    if(!player.IsLife())
    {
        player.RunClientScript("hexThrow@_UpdateWeapons", 0, 0, 0, null, null);
        return;
    }
    Map@ map = player.GetMap();
    if(!valid(map))
    {
        player.RunClientScript("hexThrow@_UpdateWeapons", 0, 0, 0, null, null);
        return;
    }
    if(px < 0 || px >= int(map.GetWidth()) || py < 0 || py >= int(map.GetHeight()))
    {
        player.RunClientScript("hexThrow@_UpdateWeapons", 0, 0, 0, null, null);
        return;
    }

    Item@ item = player.GetItemById(p2);
    if(!IsHexThrowableThere(player, item, px, py))
    {
        player.RunClientScript("hexThrow@_UpdateWeapons", 0, 0, 0, null, null);
        return;
    }

    uint16 tx = px;
    uint16 ty = py;

    // wallcheck
    uint16 pbx = tx;
    uint16 pby = ty;
    uint16 bx = tx;
    uint16 by = ty;
    if (IsGrenade(item)) {
        map.GetCrittersPath(player.HexX, player.HexY, tx, ty, 0.0f, 0, FIND_LIFE_AND_KO, null, pbx, pby, bx, by);
        tx = pbx;
        ty = pby;
    } else if (IsDistractWeapon(item)) {
        map.GetCrittersPath(player.HexX, player.HexY, tx, ty, 0.0f, 0, FIND_LIFE_AND_KO, null, pbx, pby, bx, by);
        tx = pbx;
        ty = pby;
    } else {
        player.RunClientScript("hexThrow@_UpdateWeapons", 0, 0, 0, null, null);
        return;
    }

    int tohit = HexThrowingToHit(player, tx, ty, item.Proto);
    if(tohit >= Random(1, 100))
    {
        CommenceHexThrowing(player, map, item, tx, ty);
    }
    else
    {
        uint dist = GetDistantion(player.HexX, player.HexY, tx, ty) + 1;
        map.MoveHexByDir(tx, ty, Random(0, 5), Random(1, dist / 2));
        map.GetCrittersPath(player.HexX, player.HexY, tx, ty, 0.0f, 0, FIND_LIFE_AND_KO, null, pbx, pby, bx, by);
        tx = pbx;
        ty = pby;
        CommenceHexThrowing(player, map, item, tx, ty);
        player.Say(SAY_NETMSG, "You missed.");
    }

}

void CommenceHexThrowing(Critter& cr, Map& map, Item& item, uint16 tx, uint16 ty, bool ignoreApCost = false) {
    if (ignoreApCost) {
        CommenceHexThrowingEffectAfterAnimation(cr, map, item, tx, ty, true);
    } else {
        //cr.Say(SAY_NORM, "Animate");
        cr.PlaySound('W', SOUND_WEAPON_USE, _WeaponSoundId(item.Proto, 0), '1', true);
        cr.Animate(ANIM1_UNARMED, ANIM2_THROW, null, false, true);
        cr.Wait(__Breaktime);
        uint[] values = { cr.Id, map.Id, item.Id, tx, ty };
        CreateTimeEvent(AFTER(REAL_MS(400)), "e_AnimateHexThrow", values, false);
    }
}

void CommenceHexThrowingEffectAfterAnimation(Critter& cr, Map& map, Item& item, uint16 tx, uint16 ty, bool ignoreApCost = false) {
    //  player interrupts with S or is not able to perform action
    if ((cr.Param[TO_EX_ANIM_INTERRUPT] > int(GetTick()) - ANIM_ACTION_TIME_DEFAULT) || cr.IsDead() || cr.IsKnockout()) {
        return;
    }

    uint16 weaponPid = item.GetProtoId();
    ProtoItem@ protoWeapon = GetProtoItem(weaponPid);
    uint8 use;
    switch (weaponPid) {
        case PID_SPEAR:
        case PID_SHARP_SPEAR:
        case PID_SHARPENED_POLE:
            use = 1;
            break;
        default:
            use = 0;
    }
    uint itemFlyingPid = _WeaponEffect(protoWeapon, use);
    //cr.Say(SAY_NORM, "WEAPON PID = " + weaponPid + ", FLYING PID = " + itemFlyingPid);
    if (IsGrenade(item)) {
        uint8 dir = GetDirection(cr.HexX, cr.HexY, tx, ty);
        cr.SetDir(dir);
        if(cr.Mode[MODE_HIDE] != 0) {
            if(!cr.IsPlayer() || cr.GetAccess() == ACCESS_CLIENT)
                cr.ModeBase[MODE_HIDE] = 0;
        }
        cr.Wait(__Breaktime);
        if (!ignoreApCost) {
            cr.StatBase[ST_CURRENT_AP] -= 100 * HexThrowActionPointsCost(item, cr);
        }

        uint16 fixedStartX = cr.HexX;
        uint16 fixedStartY = cr.HexY;
        uint16 fixedTargetX = tx;
        uint16 fixedTargetY = ty;
        map.MoveHexByDir(fixedTargetX, fixedTargetY, 2, 1);
        map.MoveHexByDir(fixedTargetX, fixedTargetY, 3, 1);
        uint dist = GetDistantion(fixedStartX, fixedStartY, fixedTargetX, fixedTargetY);

        uint distSourceFromTarget = GetDistantion(cr.HexX, cr.HexY, tx, ty);
        if (distSourceFromTarget > 1) {
            map.MoveHexByDir(fixedStartX, fixedStartY, dir, 1);
        }

        map.RunFlyEffect(itemFlyingPid, null, null, fixedStartX, fixedStartY, fixedTargetX, fixedTargetY);

        if(cr.Timeout[TO_BATTLE] < int(__TimeoutBattle))
            _SetTimeout(cr, TO_BATTLE, __TimeoutBattle);
        if(!map.IsTurnBased() && map.IsTurnBasedAvailability())
            map.BeginTurnBased(cr);

        int dx = fixedTargetX - fixedStartX;
        int dy = fixedTargetY - fixedStartY;
        float flyAngle = atan2(dy, dx);
        int animFlySpeed = GetAnimFlySpeedByAngle(flyAngle);
        uint animationTimeToWait = animFlySpeed * dist;

        uint[] values2 = { map.Id, tx, ty, weaponPid, cr.Id };
        CreateTimeEvent(AFTER(REAL_MS(animationTimeToWait)), "e_HexThrowExplode", values2, false);

        cr.DeleteItem(item.GetProtoId(), 1);
    } else if (IsDistractWeapon(item)) {
        uint8 dir = GetDirection(cr.HexX, cr.HexY, tx, ty);
        cr.SetDir(dir);
        cr.Wait(__Breaktime);
        if (!ignoreApCost) {
            cr.StatBase[ST_CURRENT_AP] -= 100 * HexThrowActionPointsCost(item, cr);
        }

        uint16 fixedStartX = cr.HexX;
        uint16 fixedStartY = cr.HexY;
        uint16 fixedTargetX = tx;
        uint16 fixedTargetY = ty;
        map.MoveHexByDir(fixedTargetX, fixedTargetY, 2, 1);
        map.MoveHexByDir(fixedTargetX, fixedTargetY, 3, 1);
        uint dist = GetDistantion(fixedStartX, fixedStartY, fixedTargetX, fixedTargetY);

        map.RunFlyEffect(itemFlyingPid, null, null, fixedStartX, fixedStartY, fixedTargetX, fixedTargetY);

        int dx = fixedTargetX - fixedStartX;
        int dy = fixedTargetY - fixedStartY;
        float flyAngle = atan2(dy, dx);
        int animFlySpeed = GetAnimFlySpeedByAngle(flyAngle);
        uint animationTimeToWait = animFlySpeed * dist;

        uint[] values2 = { map.Id, tx, ty, weaponPid, cr.Id };
        CreateTimeEvent(AFTER(REAL_MS(animationTimeToWait)), "e_HexThrowDistract", values2, false);

        //cr.DeleteItem(item.GetProtoId(), 1);
    } else {
        cr.RunClientScript("hexThrow@_UpdateWeapons", 0, 0, 0, null, null);
    }
}

uint e_AnimateHexThrow(array<uint>@ values) {     // critter id, map id, weapon id, toX, toY
    Critter@ cr = GetCritter(values[0]);
    if (!valid(cr)) {
        return 0;
    }
    Map@ map = GetMap(values[1]);
    if (!valid(map)) {
        return 0;
    }
    Item@ weapon =  GetItem(values[2]);
    if (!valid(weapon)) {
        return 0;
    }
    int tx = values[3];
    int ty = values[4];
    CommenceHexThrowingEffectAfterAnimation(cr, map, weapon, tx, ty, false);
    return 0;
}


uint e_HexThrowFly(array<uint>@ values) {     // map id, fromX, fromY, toX, toY, pid
    Map@ map = GetMap(values[0]);
    if (valid(map)) {
        map.RunFlyEffect(values[5], null, null, values[1], values[2], values[3], values[4]);
    }
    return 0;
}

//  not really a hex throw as it flies to a critter, could be bugged tho in engine and does not follow critter over the fly time
uint e_HexThrowFlyCritter(array<uint>@ values) {     // mapId, fromCritter, toCritter, fromX, fromY, toX, toY, pid
    Map@ map = GetMap(values[0]);
    Critter@ crFrom = GetCritter(values[1]);
    Critter@ crTo = GetCritter(values[2]);
    if (values[3] == 0 || values[4] == 0) {
        if (valid(crFrom)) {
            values[3] = crFrom.HexX;
            values[4] = crFrom.HexY;
            //Log("Readjusting x, y values for attacker: X = " + values[3] + ", Y = " + values[4]);
        } else {
            Log("Invalid throw FROM, both critter id and hex are missing.");
        }
    }
    if (values[5] == 0 || values[6] == 0) {
        if (valid(crTo)) {
            values[5] = crTo.HexX;
            values[6] = crTo.HexY;
            //Log("Readjusting x, y values for target: X = " + values[5] + ", Y = " + values[6]);
        } else {
            Log("Invalid throw TO, both critter id and hex are missing.");
        }
    }
    if (valid(map)) {
        map.RunFlyEffect(values[7], crFrom, crTo, values[3], values[4], values[5], values[6]);
    }
    return 0;
}

uint e_HexThrowExplode(array<uint>@ values)     // map id, tx, ty, pid, attacker id
{
    Map@ map = GetMap(values[0]);
    Critter@ cr = GetCritter(values[4]);
    if(!valid(map))
    {
        cr.RunClientScript("hexThrow@_UpdateWeapons", 0, 0, 0, null, null);
        return 0;
    }

    Item@ usedWeapon = cr.GetItem(values[3], -1);

    CommenceExplosionHexEx(GetCritter(values[4]), map, values[1], values[2], null, values[3], usedWeapon, 0, false, 0, false, false);
    cr.RunClientScript("hexThrow@_UpdateWeapons", 0, 0, 0, null, null);
    return 0;
}

uint e_HexThrowDistract(array<uint>@ values) {    // map id, tx, ty, pid, thrower id
    Map@ map = GetMap(values[0]);
    Critter@ cr = GetCritter(values[4]);
    if (!valid(map)) {
        cr.RunClientScript("hexThrow@_UpdateWeapons", 0, 0, 0, null, null);
        return 0;
    }

    Item@ usedWeapon = cr.GetItem(values[3], -1);

    uint tx = values[1];
    uint ty = values[2];

    cr.PlaySound('W', SOUND_WEAPON_USE, 'O', '1', true);         // '1'?
    MoveItem(usedWeapon, 1, map, tx, ty);
    usedWeapon.EventDrop(cr);
    cr.EventDropItem(usedWeapon);

    cr.RunClientScript("hexThrow@_UpdateWeapons", 0, 0, 0, null, null);

    array<Critter@> crits;
    uint foundCritters = map.GetCrittersHex(tx, ty, 12, FIND_LIFE | FIND_ONLY_NPC, crits);
    for (uint i = 0; i < foundCritters; i++) {
        /*
        if (valid(crits[i].GetCurPlane())) {
            crits[i].Say(SAY_NORM, "Current plane type = " + crits[i].GetCurPlane().Type + ", priority = " + crits[i].GetCurPlane().Priority);
        } else {
            crits[i].Say(SAY_NORM, "No current plane");
        }
        */
        if (!valid(crits[i].GetCurPlane()) || crits[i].IsCurPlane(AI_PLANE_WALK)) {
            if (!IsHumanoid(crits[i])) {
                uint8 dir = GetDirection(crits[i].HexX, crits[i].HexY, tx, ty);
                crits[i].SetDir(dir);
                crits[i].Say(SAY_EMOTE_ON_HEAD, "distracted");
                crits[i].Wait(10000 - crits[i].Stat[ST_INTELLECT]*500);
            } else {
                if (crits[i].Stat[ST_INTELLECT] < 6) {
                    uint8 dir = GetDirection(crits[i].HexX, crits[i].HexY, tx, ty);
                    crits[i].SetDir(dir);
                    crits[i].Say(SAY_NORM_ON_HEAD, "Huh?");
                    crits[i].Wait(10000 - crits[i].Stat[ST_INTELLECT]*500);
                } else {
                    crits[i].Say(SAY_NORM_ON_HEAD, "Just some rocks.");
                }
            }
        }
    }

    return 0;
}
#endif

#ifdef __CLIENT

bool Active = false;
int  LastCursor = 0;
int  itemPos = 0;
uint availableCnt = 0;
array<ItemCl@> availableItems;
bool isRock, isPlasma, isFrag, isDynacord, isMolotov, isPulse, isFire;
uint16 lastH = 0, lastM = 0, lastS = 0, lastMS = 0;

void _UpdateWeapons(int param1, int param2, int param3, string@ param4, array<int>@ param5)
{
    if(Active)
        Active = FindAvailableWeapons();
    else
        FindAvailableWeapons();
}

bool FindAvailableWeapons()
{
    CritterCl@ chosen = GetChosen();
    array<ItemCl@> allItems;

    int count = chosen.GetItems(SLOT_INV, allItems);

    isRock = isPlasma = isFrag = isDynacord = isMolotov = isPulse = false;
    availableCnt = 0;
    availableItems.resize(0);
    // 6 = plasma, frag, dynacord, molotov, pulse, fire
    availableItems.resize(THROWABLE_SIZE);
    for(int i=0; i < count; ++i)
    {
        if(valid(allItems[i]))
        {
            switch(allItems[i].GetProtoId())
            {
            case PID_FAKE_PLASMA_GRENADE:
            case PID_PLASMA_GRENADE:
                isPlasma = true;
                availableItems.removeAt(0);
                availableItems.insertAt(0, allItems[i]);
                ++availableCnt;
            break;
            case PID_FAKE_FRAG_GRENADE:
            case PID_FRAG_GRENADE:
                isFrag = true;
                availableItems.removeAt(1);
                availableItems.insertAt(1, allItems[i]);
                ++availableCnt;
            break;
            case PID_DYNACORD:
                isDynacord = true;
                availableItems.removeAt(2);
                availableItems.insertAt(2, allItems[i]);
                ++availableCnt;
            break;
            case PID_MOLOTOV_COCKTAIL:
                isMolotov = true;
                availableItems.removeAt(3);
                availableItems.insertAt(3, allItems[i]);
                ++availableCnt;
            break;
            case PID_PULSE_GRENADE:
                isPulse = true;
                availableItems.removeAt(4);
                availableItems.insertAt(4, allItems[i]);
                ++availableCnt;
            break;
            case PID_FAKE_FIRE_GRENADE:
            case PID_FIRE_GRENADE:
                isFire = true;
                availableItems.removeAt(5);
                availableItems.insertAt(5, allItems[i]);
                ++availableCnt;
            break;
            case PID_ROCK:
                isRock = true;
                availableItems.removeAt(6);
                availableItems.insertAt(6, allItems[i]);
                ++availableCnt;
            }
        }
    }

    //Message("i "+availableCnt);
    if(availableCnt > 0)
    {
        bool next = false;
        do
        {
            switch(itemPos)
            {
            case 0:
                next = isPlasma;
            break;
            case 1:
                next = isFrag;
            break;
            case 2:
                next = isDynacord;
            break;
            case 3:
                next = isMolotov;
            break;
            case 4:
                next = isPulse;
            break;
            case 5:
                next = isFire;
            break;
            case 6:
                next = isRock;
            break;
            }
            if(!next)
                itemPos = (itemPos+1)%THROWABLE_SIZE;
        }while(!next);
        return true;
    }
    else
        return false;
}

bool HexThrowingMouseDown(int click)
{
    if(!IsHexThrowingActive())
        return false;
    if(click == MOUSE_CLICK_RIGHT)
    {
        if(availableCnt > 0)
        {
            bool next = false;
            do
            {
                itemPos = (itemPos+1)%THROWABLE_SIZE;
                switch(itemPos)
                {
                    case 0:
                        next = isPlasma;
                    break;
                    case 1:
                        next = isFrag;
                    break;
                    case 2:
                        next = isDynacord;
                    break;
                    case 3:
                        next = isMolotov;
                    break;
                    case 4:
                        next = isPulse;
                    break;
                    case 5:
                        next = isFire;
                    break;
                    case 6:
                        next = isRock;
                    break;
                }
            }while(!next);
        }
        else
            itemPos = 0;
        //Message("pos "+itemPos);
    }
    else
    {
        CritterCl@ chosen = GetChosen();

        array<uint> actions;
        uint        cnt = GetChosenActions(actions);

        if(actions.length() > 0)
        {
            if(actions[0] == 1)
                TryHexThrow();
        }
        else
        {
            if(!chosen.IsAnimPlaying())
                TryHexThrow();
        }
    }

    return true;
}

bool IsHexThrowingActive() // export?
{
    return Active;
}

void HexThrowingKeyDown(uint8 key)   // export
{
    if(key == DIK_LMENU && !__ConsoleActive)
        SetHexThrowing(true);
}

void HexThrowingKeyUp(uint8 key)   // export
{
    if(key == DIK_LMENU)
        SetHexThrowing(false);
}

void HexThrowingInputLost() // export
{
    SetHexThrowing(false);
}

void SetHexThrowing(bool enabled)
{
    if(enabled)
    {
        //Active = true;
        LastCursor = ::GetCurrentCursor();
        if(LastCursor == CURSOR_USE_WEAPON)
        {
            if(FindAvailableWeapons())
                Active = true;
            else
                Active = false;
        }

        //ChangeCursor(1);         // todo
    }
    else
    {
        Active = false;
        //ChangeCursor(LastCursor);
    }
}

void RenderHexThrowing() // export
{
    if(!IsHexThrowingActive())
        return;
    CritterCl@ chosen = GetChosen();
    if(!valid(chosen))
        return;

    if(!Active)
        return;

    uint16 hx = 0, hy = 0;
    if(!GetMonitorHex(__MouseX, __MouseY, hx, hy))
        return;
    // todo: check for sanity?

    //throw indicator
    ItemCl@ item = availableItems[itemPos];
    int radius = HexThrowGetMaxRange(chosen, item);

    array<uint16> radiusVert =
    {
        chosen.HexX, chosen.HexY,
        chosen.HexX, chosen.HexY,
        chosen.HexX, chosen.HexY,
        chosen.HexX, chosen.HexY,
        chosen.HexX, chosen.HexY,
        chosen.HexX, chosen.HexY
    };
    MoveHexByDir(radiusVert[0], radiusVert[1], 0, radius);
    MoveHexByDir(radiusVert[2], radiusVert[3], 1, radius);
    MoveHexByDir(radiusVert[4], radiusVert[5], 2, radius);
    MoveHexByDir(radiusVert[6], radiusVert[7], 3, radius);
    MoveHexByDir(radiusVert[8], radiusVert[9], 4, radius);
    MoveHexByDir(radiusVert[10], radiusVert[11], 5, radius);

    #define RANGE_BORDER      (0x77FF7777)

    DrawHexArea(radiusVert, false, RANGE_BORDER);

    if(!IsHexThrowableThere(chosen, item, hx, hy))
        return;

    //splash indicator
    uint splashRadius = 2;
    if(item.GetProtoId() == PID_FIRE_GRENADE || item.GetProtoId() == PID_FAKE_FIRE_GRENADE)
        splashRadius = 3;
    else if(item.GetProtoId() == PID_DYNACORD)
        splashRadius = 4;

    array<uint16> splashRadiusVert =
    {
        hx, hy,
        hx, hy,
        hx, hy,
        hx, hy,
        hx, hy,
        hx, hy
    };
    MoveHexByDir(splashRadiusVert[0], splashRadiusVert[1], 0, splashRadius);
    MoveHexByDir(splashRadiusVert[2], splashRadiusVert[3], 1, splashRadius);
    MoveHexByDir(splashRadiusVert[4], splashRadiusVert[5], 2, splashRadius);
    MoveHexByDir(splashRadiusVert[6], splashRadiusVert[7], 3, splashRadius);
    MoveHexByDir(splashRadiusVert[8], splashRadiusVert[9], 4, splashRadius);
    MoveHexByDir(splashRadiusVert[10], splashRadiusVert[11], 5, splashRadius);

    #define SPLASH_RADIUS_BORDER      (0x77FF0000)

    DrawHexArea(splashRadiusVert, false, SPLASH_RADIUS_BORDER);

    // now we actually render the info
    int x = 0, y = 0;
    if(!GetHexPos(hx, hy, x, y))
        return;                           // should not happen

    uint tohit = ::HexThrowingToHit(chosen, hx, hy, item.Proto);
    int  ap = ::HexThrowActionPointsCost(item, chosen);
    bool hasAp = ap <= chosen.Stat[ST_CURRENT_AP];

    // todo: adjust offsets
    int sprite = LoadSprite(item.Proto.PicInv, 0);
    ::DrawSprite(sprite, -1, x - 50, y - 15, 30, 30, false, false, COLOR_WHITE);
    DrawText(tohit + "%", x + 20, y - 10, 40, 20, hasAp ? COLOR_GREEN : COLOR_RED, FONT_TYPE_DEFAULT, FONT_FLAG_BORDERED);
    DrawText(ap + " AP", x + 20, y, 40, 20, hasAp ? COLOR_LGRAY : COLOR_RED, FONT_TYPE_DEFAULT, FONT_FLAG_BORDERED);
    DrawText(item.GetCount()+"", x - 30, y-10, 40, 20, hasAp ? COLOR_LGRAY : COLOR_RED, FONT_TYPE_DEFAULT, FONT_FLAG_BORDERED);

}

void TryHexThrow()
{
    // sanity check
    if(!IsHexThrowingActive())
        return;
    CritterCl@ chosen = GetChosen();
    if(!valid(chosen))
        return;

    uint16 hx = 0, hy = 0;
    if(!GetMonitorHex(__MouseX, __MouseY, hx, hy))
        return;
    // todo: check for sanity?

    ItemCl@ item = availableItems[itemPos];

    if(!IsHexThrowableThere(chosen, item, hx, hy))
        return;

    if(::HexThrowActionPointsCost(item, chosen) > chosen.Stat[ST_CURRENT_AP])
    {
        array<uint> actions;
        uint        cnt = GetChosenActions(actions);
        if(actions.length() > 0 && actions[0] == CHOSEN_MOVE)
        {
            uint[] action = { CHOSEN_NONE, 0, 0, 0, 0, 0, 0 };
            SetChosenActions(action);
        }
        return;
    }

    // throw
    if(CheckTimeDiff(lastH, lastM, lastS, lastMS))
        RunServerScriptUnsafe("hexThrow@unsafe_HexThrow", hx, hy, item.Id, null, null);
}

bool CheckTimeDiff(uint16 lh, uint16 lm, uint16 ls, uint16 lms)
{
    uint16 undef;
    uint16 hour = 0;
    uint16 second = 0;
    uint16 msecond = 0;
    uint16 minute = 0;

    GetTime(undef, undef, undef, undef, hour, minute, second, msecond);

    lastH = hour;
    lastM = minute;
    lastS = second;
    lastMS = msecond;

    if(lh > hour)
        hour += 24;
    if(lm > minute)
    {
        minute += 60;
        hour--;
    }
    if(ls > second)
    {
        second += 60;
        minute--;
    }
    if(lms > msecond)
    {
        msecond += 1000;
        second--;
    }

    uint td = (second-ls)*1000;
    td = (msecond-lms+td);

    if(td >= __Breaktime || lh != hour || lm != minute)
        return true;
    else
    {
        lastH = lh;
        lastM = lm;
        lastS = ls;
        lastMS = lms;
        return false;
    }
}

#endif
